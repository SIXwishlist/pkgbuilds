# Maintainer: Jacob Cook <jacob at citizenweb dot io>

pkgname=arkos-core
pkgver=0.8.2
pkgrel=1
pkgdesc="Python system management libraries for arkOS"

arch=('any')
url="https://arkos.io/"
license=('GPLv3')
depends=('binutils' 'dbus-glib' 'parted' 'python' 'python2' 'arkos-configs' 'python-ntplib'
    'python-nginx' 'python-dbus' 'python-gobject' 'python-cryptsetup' 'python-pyparted'
    'python-pyldap' 'python-psutil' 'python-netifaces' 'python-gitpython' 'python-gnupg'
    'python-pacman' 'python-click' 'python-requests' 'python-cryptography' 'nginx' 'git'
    'python-miniupnpc' 'openldap' 'python-semantic-version' 'fail2ban' 'supervisor' 'ruby'
    'nodejs' 'npm' 'ntp' 'cronie' 'nss-pam-ldapd' 'python-free-tls-certificates' 'python-pip'
    'python2-pip' 'python-passlib' 'python-wheel' 'python2-wheel')
makedepends=('python')
provides=('arkos-core')
install='arkos-core.install'
source=("$pkgname-$pkgver.tar.gz"::"https://git.coderouge.co/arkOS/core/repository/archive.tar.gz?ref=$pkgver")
md5sums=('83a9afa04b9d1bc86003bdf7f46750bf')


build() {
  cd "$srcdir"
  tar xvzf "$pkgname-$pkgver.tar.gz"
  cd "core-"*
  python setup.py bdist_wheel
}

package() {
  cd "$srcdir/core-"*
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/arkos_core-$pkgver-py3-none-any.whl
}
