# Maintainer: Nikos Skalkotos <skalkoto at gmail dot com>
# Contributor: Achilleas Pipinellis <axilleas archlinux info>
# Contributor: StÃ©phane Gaudreault <stephane@archlinux.org>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: GÃ¶kmen GÃ¶rgen <gkmngrgn at gmail dot com>
# Contributor: Baurzhan Muftakhidinov <baurthefirst (at) gmail (dot) com>

_pkgname=pyparted
pkgname=python2-pyparted
pkgver=3.10.1
pkgrel=2
pkgdesc="Python module for GNU parted"
url="https://github.com/dcantrell/pyparted"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL2')
depends=('python2' 'parted>=3.0')
makedepends=('pkg-config')
conflicts=('pyparted-git')
replaces=('pyparted')
source=("https://github.com/dcantrell/${_pkgname}/archive/${_pkgname}-${pkgver}.tar.gz")

build() {
  cd "${srcdir}/${_pkgname}-${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
sha512sums=('d2842ba919808ccb757f6852e17c725b7db6e94db31548366fbdf2376960f8cf6ce239cb33282b4eca02f20a94f85bd58e8fc1d40834670c9684f5903c3515e5')
